plugins {
    id 'java-library'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)' apply false
    id 'org.spongepowered.mixin' version '0.7.+' apply false
    id 'com.modrinth.minotaur' version '2.+' apply false
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'org.spongepowered.mixin'

    group = mod_group_id
    version = mod_version

    base {
        archivesName = mod_id
    }

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
    minecraft {
        mappings channel: mapping_channel, version: mapping_version
        copyIdeResources = true
        runs {
            configureEach {
                workingDirectory project.file('run')
                property 'forge.logging.markers', 'REGISTRIES'
                property 'forge.logging.console.level', 'debug'
                mods {
                    "${mod_id}" {
                        if (project.name != 'common') {
                            source project(":common").sourceSets.main
                        }
                        source sourceSets.main
                    }
                }
            }

            client {
                property 'forge.enabledGameTestNamespaces', mod_id
            }

            server {
                property 'forge.enabledGameTestNamespaces', mod_id
                args '--nogui'
            }

            gameTestServer {
                property 'forge.enabledGameTestNamespaces', mod_id
            }

            data {
                workingDirectory project.file('run-data')
                args '--mod', mod_id, '--all', '--output', project(':common').file('src/generated/resources/'), '--existing', file('src/main/resources/')
            }
        }
    }

    sourceSets.main.resources { srcDir 'src/generated/resources' }

    repositories {
        maven {
            name 'Create, Ponder, Flywheel (6.0)'
            url = 'https://maven.createmod.net'
        }
        maven {
            name 'Create, Flywheel (5.1)'
            url 'https://modmaven.dev/'
            content {
                includeGroup 'com.simibubi.create'
                includeGroup 'com.jozufozu.flywheel'
            }
        }
        maven {
            name 'Registrate'
            url 'https://maven.tterrag.com'
            content {
                includeGroup 'com.tterrag.registrate'
            }
        }
        maven {
            name 'Cloth Config'
            url 'https://maven.shedaniel.me'
            content {
                includeGroup 'me.shedaniel.cloth'
            }
        }
        maven {
            name 'Curse Maven'
            url 'https://cursemaven.com'
            content {
                includeGroup 'curse.maven'
            }
        }
        maven {
            name 'JEI, Immersive Engineering'
            url 'https://maven.blamejared.com'
            content {
                includeGroup 'mezz.jei'
                includeGroup 'blusunrize.immersiveengineering'
            }
        }
        maven {
            name 'Conditional Mixin'
            url 'https://maven.fallenbreath.me/releases'
            content {
                includeGroup 'me.fallenbreath'
            }
        }
        maven {
            name = 'GeckoLib (for Warium runtime)'
            url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
            content {
                includeGroupByRegex("software\\.bernie.*")
            }
        }
        flatDir {
            dir rootProject.file('libs')
        }
    }

    dependencies {
        minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
        
        jarJar(group: 'me.fallenbreath', name: 'conditional-mixin-forge', version: '[0.6.4,)')
        compileOnly fg.deobf("me.fallenbreath:conditional-mixin-forge:0.6.4")

        api fg.deobf("me.shedaniel.cloth:cloth-config-forge:11.1.136")
        
        runtimeOnly fg.deobf("software.bernie.geckolib:geckolib-forge-${mc_version}:4.8.2")
        implementation fg.deobf("blank:Warium-${warium_version}:${warium_version}")

        annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
        
        compileOnly fg.deobf("blank:WariumVS-${valkyrien_warium_version}:${valkyrien_warium_version}")

        compileOnly fg.deobf("com.tterrag.registrate:Registrate:MC1.20-1.3.3")

        compileOnly fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}")
        compileOnly fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}")

        compileOnly fg.deobf("blusunrize.immersiveengineering:ImmersiveEngineering:${mc_version}-${immersiveengineering_version}")
    }

    mixin {
        if (gradle.startParameter.taskNames.every {!it.contains('runData')}) {
            add sourceSets.main, "${mod_id}.mixins.refmap.json"
            config "${mod_id}.mixins.json"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs += ["-Xlint:-removal"]
    }

    tasks.named('processResources', ProcessResources).configure {
        var replaceProperties = [
                mc_version: mc_version,
                forge_version: forge_version,
                loader_version_range: loader_version_range,
                mod_group_id: mod_group_id,
                mod_id: mod_id,
                mod_name: mod_name,
                mod_license: mod_license,
                mod_version: mod_version,
                mod_authors: mod_authors,
                credits: credits,
                mod_description: mod_description,
                warium_version: warium_version,
                valkyrien_warium_version: valkyrien_warium_version,
                create_version: create_version,
                immersiveengineering_version: immersiveengineering_version,
                jei_version: jei_version
        ]
        inputs.properties replaceProperties

        filesMatching(['META-INF/mods.toml', 'pack.mcmeta', mod_id + '.mixins.json']) {
            expand replaceProperties + [project: project]
        }
    }

    tasks.register('cleanLibs', Delete) {
        delete fileTree(dir: "$buildDir/libs", include: '*.jar')
    }

    if (project.name != 'common') {
        jarJar.enable()

        gradle.taskGraph.whenReady { graph ->
            if (graph.hasTask(tasks.build)) {
                // delete file("build/tmp/compileJava/${mod_id}.mixins.refmap.json")
                delete layout.buildDirectory
            }
        }

        tasks.named('jarJar', Jar).configure {
            from(project(":common").sourceSets.main.output)
            archiveClassifier = project.name
        }

        tasks.register('mergeRefmaps') {
            dependsOn project(':common').tasks.getByName('compileJava')
            
            doLast {
                def commonRefmap = project(':common').file("build/tmp/compileJava/${mod_id}.mixins.refmap.json")
                def create6Refmap = file("build/tmp/compileJava/${mod_id}.mixins.refmap.json")
                
                if (commonRefmap.exists() && create6Refmap.exists()) {
                    def commonJson = new groovy.json.JsonSlurper().parse(commonRefmap)
                    def create6Json = new groovy.json.JsonSlurper().parse(create6Refmap)
                    
                    commonJson.mappings.each { key, value ->
                        if (!create6Json.mappings.containsKey(key)) {
                            create6Json.mappings[key] = value
                        }
                    }
                    
                    commonJson.data.each { key, value ->
                        if (!create6Json.data.containsKey(key)) {
                            create6Json.data[key] = value
                        } else {
                            value.each { innerKey, innerValue ->
                                if (!create6Json.data[key].containsKey(innerKey)) {
                                    create6Json.data[key][innerKey] = innerValue
                                }
                            }
                        }
                    }
                    
                    create6Refmap.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(create6Json))
                }
            }
        }

        tasks.named('classes').configure {
            finalizedBy 'mergeRefmaps'
        }

        tasks.named('jar', Jar).configure {
            dependsOn 'cleanLibs'
            from(project(":common").sourceSets.main.output)
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
            
            manifest {
                attributes([
                        'Specification-Title'     : mod_id,
                        'Specification-Vendor'    : mod_authors,
                        'Specification-Version'   : '1',
                        'Implementation-Title'    : project.name,
                        'Implementation-Version'  : project.jar.archiveVersion,
                        'Implementation-Vendor'   : mod_authors,
                        'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
                ])
            }

            finalizedBy 'reobfJar'
        }

        tasks.register('copyModJar', Copy) {
            dependsOn build
            from("$buildDir/libs/") {
                include "*-${project.name}.jar"
            }
            into System.getenv('MC_TESTS_DIR')
            onlyIf {
                file(System.getenv('MC_TESTS_DIR')).exists()
            }
        }

        build.finalizedBy(copyModJar)
    }
}

jar {
    enabled = false
}